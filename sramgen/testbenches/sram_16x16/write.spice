* SRAM 16x16 simplified testbench

.lib /Users/rahul/acads/research/sky130/pdk/skywater-pdk/libraries/sky130_fd_pr/latest/models/sky130.lib.spice tt

.include ../../lib/sram_sp_cell/sky130_fd_bd_sram__sram_sp_cell.spice
.include ../../lib/sramgen_control/sramgen_control.spice
.include ../../lib/sramgen_sp_sense_amp/sramgen_sp_sense_amp.sp

.include ../../build/spice/sram_16x16.spice

.param cload='2p'
.param vdd='1.8'
.param tr='1n'
.param tf='1n'

Xsram
+ vdd 0 clk
* din
+ vdd vdd vdd 0    vdd 0 vdd 0    0 vdd vdd vdd    vdd 0 0 vdd
* din_b
+ 0 0 0 vdd        0 vdd 0 vdd    vdd 0 0 0        0 vdd vdd 0
* dout
+ dout_3 dout_2 dout_1 dout_0
* we cs
+ we vdd
* addr
+ vdd 0 vdd 0 0 0
* addr_b
+ 0 vdd 0 vdd vdd vdd 
+ sramgen_sram_16x16

Vwe we 0 dc 0 PULSE(0 {vdd} 15p {tr} {tf} 200n 400n 0)
Vclk clk 0 dc 0 PULSE(0 {vdd} 10p {tr} {tf} 100n 200n 0)
Vdd vdd 0 {vdd}

.control
tran 1n 240n 0u
wrdata write.dat
+ v(clk)
+ v(we)
+ v(dout_3)
+ v(dout_2)
+ v(dout_1)
+ v(dout_0)
+ v(xsram.pc_b)
+ v(xsram.wl_en)
+ v(xsram.wr_drv_en)
+ v(xsram.sense_amp_en)
+ v(xsram.bl_4)
+ v(xsram.bl_5)
+ v(xsram.bl_6)
+ v(xsram.bl_7)
+ v(xsram.br_4)
+ v(xsram.br_5)
+ v(xsram.br_6)
+ v(xsram.br_7)
+ v(xsram.bl_out_0)
+ v(xsram.br_out_0)
+ v(xsram.bl_out_1)
+ v(xsram.br_out_1)
+ v(xsram.bl_out_2)
+ v(xsram.br_out_2)
+ v(xsram.bl_out_3)
+ v(xsram.br_out_3)
unset askquit
quit 0
.endc

.end

