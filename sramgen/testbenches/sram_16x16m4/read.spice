* SRAM 4x4 read testbench

.lib /Users/rahul/acads/research/sky130/pdk/skywater-pdk/libraries/sky130_fd_pr/latest/models/sky130.lib.spice ff

.include ../../lib/sram_sp_cell/sky130_fd_bd_sram__sram_sp_cell.spice
.include ../../lib/sramgen_sp_sense_amp/sramgen_sp_sense_amp.spice
.include ../../lib/sramgen_control/sramgen_control_simple.spice
.include ../../lib/openram_dff/openram_dff.spice
.include ../../build/ngspice/sram_16x16m4.spice

.param vdd='1.8'
.param tr='100p'
.param tf='100p'

* .SUBCKT sramgen_sram_16x16m4 
* + vdd vss clk din_3 din_2 din_1 din_0 dout_3 dout_2 dout_1 dout_0 we addr_5 addr_4 addr_3 addr_2 addr_1 addr_0 
Xsram
* vdd vss clk 
+ vdd vss clk
* din[3:0]
+ vdd vss vdd vss
* dout[3:0]
+ dout_3 dout_2 dout_1 dout_0
* we
+ vss
* addr[5:0]
+ vdd vss vss vss vss vdd
+ sramgen_sram_16x16m4

Vclk clk 0 dc 0 PULSE(0 {vdd} 10p {tr} {tf} 2n 4n 0)
Vss vss 0 dc 0
Vdd vdd 0 {vdd}

.ic
+ v(xsram.xbitcells.xbitcell_8_0.q)=1.8
+ v(xsram.xbitcells.xbitcell_8_0.qb)=0
+ v(xsram.xbitcells.xbitcell_8_1.q)=1.8
+ v(xsram.xbitcells.xbitcell_8_1.qb)=0
+ v(xsram.xbitcells.xbitcell_8_2.q)=1.8
+ v(xsram.xbitcells.xbitcell_8_2.qb)=0.0
+ v(xsram.xbitcells.xbitcell_8_3.q)=1.8
+ v(xsram.xbitcells.xbitcell_8_3.qb)=0.0

.control
tran 5p 18n 0n
wrdata read.dat
+ v(vdd)
+ v(vss)
+ v(clk)
+ v(dout_3)
+ v(dout_2)
+ v(dout_1)
+ v(dout_0)
+ v(xsram.wl_3)
+ v(xsram.wl_2)
+ v(xsram.wl_1)
+ v(xsram.wl_0)
+ v(xsram.bl_3)
+ v(xsram.bl_2)
+ v(xsram.bl_1)
+ v(xsram.bl_0)
+ v(xsram.br_3)
+ v(xsram.br_2)
+ v(xsram.br_1)
+ v(xsram.br_0)
+ v(xsram.bl_read_1)
+ v(xsram.br_read_1)
+ v(xsram.bl_read_0)
+ v(xsram.br_read_0)
+ v(xsram.bank_addr_0)
+ v(xsram.bank_addr_b_0)
+ v(xsram.sense_amp_en)
+ v(xsram.xsense_amp_array.data_b_0)
.endc

.end

