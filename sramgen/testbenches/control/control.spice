* Testbench for control logic

.lib /Users/rahul/acads/research/sky130/pdk/skywater-pdk/libraries/sky130_fd_pr/latest/models/sky130.lib.spice tt

.param cload='2p'
.param vdd='1.8'
.param tr='1n'
.param tf='1n'

.subckt control_logic_inv0 din din_b vdd vss
Xp din_b din vdd vdd sky130_fd_pr__pfet_01v8 w='1' l='0.15'
Xn din_b din vss vss sky130_fd_pr__nfet_01v8 w='0.6' l='0.15'
.ends

.subckt control_logic_delay_chain din dout vdd vss
Xinv0 din tmp0 vdd vss control_logic_inv0
Xinv1 tmp0 tmp1 vdd vss control_logic_inv0
Xinv2 tmp1 tmp2 vdd vss control_logic_inv0
Xinv3 tmp2 dout vdd vss control_logic_inv0
.ends

.subckt control_logic_nand0 a b y vdd vss
Xp1 y a vdd vdd sky130_fd_pr__pfet_01v8 w='2' l='0.15'
Xp2 y b vdd vdd sky130_fd_pr__pfet_01v8 w='2' l='0.15'
Xn1 y a int vss sky130_fd_pr__nfet_01v8 w='1' l='0.15'
Xn2 int b vss vss sky130_fd_pr__nfet_01v8 w='1' l='0.15'
.ends

.subckt control_logic_and0 a b y vdd vss
Xnand a b y_bar vdd vss control_logic_nand0
Xinv y_bar y vdd vss control_logic_inv0
.ends

.subckt control_logic clk cs we pc wl_en write_driver_en sense_en vdd vss
Xand0 clk cs pc vdd vss control_logic_and0
Xinv_clk clk clk_b vdd vss control_logic_inv0
Xand1 clk_b cs clk_b_gated vdd vss control_logic_and0
Xdelay_wl_en clk_b_gated wl_en vdd vss control_logic_delay_chain
Xand2 clk_b_gated we write_driver_en vdd vss control_logic_and0
CL sense_en vss 10f
.ends

Xctrl clk cs we pc wl_en write_driver_en sense_en vdd 0 control_logic

Cpc pc 0 {cload}
Cwl_en wl_en 0 {cload}
Cwrite_driver_en write_driver_en 0 {cload}
Csense_en sense_en 0 {cload}

Vclk clk 0 dc 0 PULSE(0 {vdd} 0 {tr} {tf} 1u 2u 0)
Vcs cs 0 dc 0 PULSE(0 {vdd} 0 {tr} {tf} 100 200 0)
Vwe we 0 dc 0 PULSE(0 {vdd} 0 {tr} {tf} 4u 8u)

Vdd vdd 0 {vdd}

.control
tran 100p 8.2u 0u
wrdata control.dat
+ v(clk)
+ v(cs)
+ v(we)
+ v(pc)
+ v(wl_en)
+ v(write_driver_en)
+ v(sense_en)
unset askquit
quit 0
.endc

.end

